plugins {
    id "java"
    id "application"
//    id "com.github.johnrengelman.shadow" version "7.1.0"
    id 'org.beryx.jlink' version '2.23.5'
}

group 'smlauncher'
mainClassName = 'smlauncher.StarMadeLauncher'
version = '3.1.0'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    gradlePluginPortal()
}

dependencies {
    implementation 'com.formdev:flatlaf:3.0'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.codehaus.plexus:plexus-archiver:4.9.0'
    implementation 'org.jasypt:jasypt:1.9.3'
    implementation 'org.json:json:20231013'
    implementation 'it.unimi.dsi:fastutil:8.5.12'
}

java {
    modularity.inferModulePath = true
}

jar {
    manifest {
        attributes 'Main-Class': 'smlauncher.StarMadeLauncher'
    }
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    destinationDirectory.set(file("./release-builds/"))

    archiveFileName = 'StarMade-Launcher.jar'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    from(sourceSets.main.output)
    from('resources/Updater.jar') {
        into 'Updater.jar'
    }
}

def currentOS = org.gradle.nativeplatform.platform.internal.DefaultNativePlatform.currentOperatingSystem;
def platform
if (currentOS.isWindows()) {
    platform = 'win'
} else if (currentOS.isLinux()) {
    platform = 'linux'
} else if (currentOS.isMacOsX()) {
    platform = 'mac'
}


jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    jpackage {
        skipInstaller = true
        imageOptions = ['--icon', 'assets/starmade.ico']
        installerOptions = ['--win-menu', '--win-shortcut']
    }
    launcher {
        name = 'StarMade Launcher'
    }
}