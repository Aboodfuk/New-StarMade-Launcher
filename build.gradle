plugins {
	id "java"
	id "application"
	id "org.panteleyev.jpackageplugin" version "1.6.0"
	id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'smlauncher'
mainClassName = 'smlauncher.StarMadeLauncher'
version = '3.1.0'

sourceCompatibility = 17
targetCompatibility = 17

application {
	mainClassName = 'smlauncher.StarMadeLauncher'
	mainModule = 'smlauncher'
}

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
	maven { url "https://plugins.gradle.org/m2/" }
	gradlePluginPortal()
}

dependencies {
	implementation('org.codehaus.plexus:plexus-archiver:4.10.0') {
		exclude(group: 'org.codehaus.plexus', module: 'plexus-io')
	}
	implementation 'commons-io:commons-io:2.11.0'
	implementation 'org.json:json:20231013'
	implementation 'it.unimi.dsi:fastutil:8.5.12'
	implementation 'org.jasypt:jasypt:1.9.3'
	implementation 'com.formdev:flatlaf:3.0'
	implementation 'org.panteleyev:jpackage-gradle-plugin:1.6.0'
}

java {
	modularity.inferModulePath = true
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
		vendor = JvmVendorSpec.ADOPTIUM
	}
}

var os = System.getProperty('os.name').toLowerCase()
if(os.contains('win')) {
	os = 'windows-latest'
} else if(os.contains('mac')) {
	os = 'macos-latest'
} else {
	os = 'linux-latest'
}

jar {
	manifest {
		attributes 'Main-Class': 'smlauncher.StarMadeLauncher'
	}
	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
	duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
	archiveFileName = 'StarMade-Launcher.jar'
	from(sourceSets.main.output)
	from('resources/Updater.jar') {
		into 'Updater.jar'
	}
}

clean {
	delete 'release-builds'
}

jpackage {
    dependsOn(clean, jar)
    input  = "./build/libs/"
    destination = "./release-builds/" + os
    mainJar = "StarMade-Launcher.jar"
    mainClass = "smlauncher.StarMadeLauncher"
	appName = "StarMade-Launcher"
	appVersion = project.version.toString()
	vendor = "smlauncher"
	copyright = "Copyright (c) 2024 Schine GmbH"
	runtimeImage = System.getProperty("java.home")
	type = "app-image"
	mac {
		icon = "./src/main/resources/starmade.icns"
	}
	windows {
		icon = "./src/main/resources/starmade.ico"
	}
	linux {
		icon = "./src/main/resources/starmade.png"
	}
}